plugins {
	id 'fabric-loom' version '1.3-SNAPSHOT'
	id 'maven-publish'
	id "com.modrinth.minotaur" version "2.+"
	id 'net.darkhax.curseforgegradle' version "1.1.16"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven {
		name = "Parchment"
		url = 'https://maven.parchmentmc.org'
	}
	maven {
		name = "Porting Lib"
		url = "https://mvn.devos.one/snapshots/"
	}
	maven {
		name = "CCA + PAL"
		url = 'https://maven.ladysnake.org/releases'
	}
	maven {
		url "https://maven.jamieswhiteshirt.com/libs-release"
		content {
			includeGroup "com.jamieswhiteshirt"
		}
	}
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven { url "https://jitpack.io/" }
	maven { url 'https://maven.blamejared.com' }
	maven { url "https://maven.shedaniel.me" }
	maven { url "https://maven.terraformersmc.com" }
	maven { url "https://cursemaven.com" }
	maven { url "https://maven.bai.lol" }
	maven { url 'https://maven.nucleoid.xyz' }
	maven { url 'https://maven.wispforest.io' } //owo lib

	mavenLocal()
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-1.20.1:2023.06.26@zip")
	}
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Porting lib
	modImplementation("io.github.fabricators_of_create.Porting-Lib:accessors:${project.port_lib_version}")
	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:attributes:${project.port_lib_version}"))
	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:base:${project.port_lib_version}"))
	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:client_events:${project.port_lib_version}"))
	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:common:${project.port_lib_version}"))
	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:core:${project.port_lib_version}"))
	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:entity:${project.port_lib_version}"))
	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:extensions:${project.port_lib_version}"))
	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:lazy_registration:${project.port_lib_version}"))
	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:loot:2.1.1102+1.20-entity-refactor")) //Using a more recent version... hopefully fine?
	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:mixin_extensions:${project.port_lib_version}"))
	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:models:${project.port_lib_version}"))
	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:registries:${project.port_lib_version}"))
	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:tags:${project.port_lib_version}"))
	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:tool_actions:${project.port_lib_version}"))
	modImplementation("io.github.fabricators_of_create.Porting-Lib:transfer:${project.port_lib_version}")
	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:utility:${project.port_lib_version}"))

	// CCA + PAL
	modImplementation(include("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cardinal_components_version}"))
	modImplementation(include("io.github.ladysnake:PlayerAbilityLib:${project.pal_version}"))

	// Trinkets
	modCompileOnly(modRuntimeOnly("dev.emi:trinkets:${project.trinkets_version}"))

	// Reach entity attributes
	modImplementation("com.jamieswhiteshirt:reach-entity-attributes:${project.reach_lib_version}")

	// Projectile Damage Attribute
	modImplementation(include("maven.modrinth:projectile-damage-attribute:${project.projectile_damage}-fabric"))

	// Additional Entity Attributes
	modImplementation(include("maven.modrinth:AdditionalEntityAttributes:${project.additional_attributes}"))

	// Mixin Extras + Mixin^2 for mod compat
	include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:0.2.0-beta.10")))
	include(implementation(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-fabric:0.0.2")))

	// Fakerlib
	modImplementation("dev.shadowsoffire.placebo:placebo-fabric-1.20.1:${project.fakerlib_version}")

	// REI + Arch
	modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
	modRuntimeOnly "dev.architectury:architectury-fabric:${project.arch_version}"
	modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}"
	modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${project.rei_version}"

	// Testing polymer incompat
	modCompileOnly("eu.pb4:sgui:1.2.2+1.20")
	modCompileOnly("eu.pb4:placeholder-api:2.1.3+1.20.1")
	modCompileOnly('xyz.nucleoid:server-translations-api:2.0.0+1.20')
	modCompileOnly('eu.pb4:polymer-core:0.5.15+1.20.1')
	modCompileOnly('eu.pb4:polymer-resource-pack:0.5.15+1.20.1')
	modCompileOnly('eu.pb4:polymer-virtual-entity:0.5.15+1.20.1')
	modCompileOnly('eu.pb4:predicate-api:0.2.1+1.20.1')
	modCompileOnly("eu.pb4:common-protection-api:1.0.0")

	modCompileOnly("me.lucko:fabric-permissions-api:0.2-SNAPSHOT")
	modCompileOnly "curse.maven:universal-graves-497175:4754378"

	//modImplementation "maven.modrinth:obscure-api:16"
	//modImplementation "maven.modrinth:aquamirae:ouUaC54p"

	modCompileOnly "curse.maven:puffish-skills-835091:4809638"

	modImplementation("io.wispforest:lavender:0.1.0-pre.6+1.20")

	modImplementation("io.wispforest:owo-lib:0.11.0+1.20")
	modImplementation "curse.maven:conjuring-422310:4580578"

}

loom {
	accessWidenerPath = file("src/main/resources/zenith_attributes.aw")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "zenith-attributes"
	versionType = "beta"
	versionName = "Zenith Attributes " + project.version
	uploadFile = remapJar
	gameVersions = ["1.20.1"]
	loaders = ["fabric"]
	changelog = "[Changelog](https://github.com/TheWinABagel/ZenithAttributes/blob/master/changelog.md)"
	//debugMode = true
	dependencies {
		required.project "fabric-api"
		required.project("fakerlib")
		embedded.project("porting_lib")
	}
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
	apiToken = System.getenv("cf_token")
	//debugMode = true
	def mainFile = upload(910078, remapJar)
	mainFile.changelog = "https://github.com/TheWinABagel/ZenithAttributes/blob/master/changelog.md"
	mainFile.releaseType = "beta"
	//mainFile.addJavaVersion("17")
	mainFile.addGameVersion("1.20.1")
	mainFile.addModLoader("fabric")
	mainFile.displayName = "zenith_attributes-" + project.version
}